plugins {
	id 'groovy'
	id 'java-gradle-plugin'

	// for publishing to the plugin portal
	id 'com.gradle.plugin-publish' version '0.11.0'

	// for publishing snapshots
	id 'maven-publish'

	id 'idea'
	id 'eclipse'
}

repositories {
	mavenCentral()
}

ext {
	pluginId = 'com.olafmertens.xjc'
	pluginVersion = '1.0.0'
}


group = 'com.olafmertens.gradle'
version = ext.pluginVersion

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
	compile gradleApi()
	compile localGroovy()
}

buildDir = 'target'

gradlePlugin {
	plugins {
		xjcPlugin {
			id = project.pluginId
			implementationClass = 'com.olafmertens.gradle.xjc.XjcPlugin'
		}
	}
}

pluginBundle {
	website = 'https://github.com/olafmertens/gradle-xjc-plugin'
	vcsUrl = 'https://github.com/olafmertens/gradle-xjc-plugin'
	tags = ['jaxb','xjc']

	plugins {
		xjcPlugin {
			id = project.pluginId
			displayName = 'JAXB binding model generation using the XJC tool'
			description = 'JAXB binding model generation using the XJC tool'
		}
	}
}

if ( version.toString().endsWith( '-SNAPSHOT' ) ) {
	tasks.publishPlugins.enabled = false
}

task compile
tasks.compile.dependsOn tasks.compileJava, tasks.compileTestJava

publishing {
	repositories {
		mavenLocal()
	}
}

wrapper {
	// To upgrade the version of gradle used in the wrapper, run:
	//     ./gradlew wrapper --gradle-version NEW_VERSION
	distributionType = Wrapper.DistributionType.ALL
}
